app budgetbuddy {
  wasp: {
    version: "^0.11.1"
  },
  title: "budgetbuddy",
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {},
    },
    onAuthFailedRedirectTo: "/login"
  },
  db: {
    system: PostgreSQL
  },
  dependencies: [
    ("antd", "5.8.2"),
    ("dayjs", "1.11.9"),
    ("rc-virtual-list", "3.5.3")
  ]
}

entity User {=psl 
  id        Int @id @default(autoincrement())
  username  String @unique
  password  String
  categories Category[]
  transactions Transaction[]
  tags Tag[]
psl=}

entity Category {=psl 
  name String
  userId Int
  user User @relation(fields: [userId], references: [id])
  @@id([userId, name])
  transactions Transaction[]
psl=}

entity Tag {=psl 
  id Int @id @default(autoincrement())
  name String
  userId Int
  user User @relation(fields: [userId], references: [id])
  transactions Transaction[]
  @@unique([userId, name])
psl=}

entity Transaction {=psl 
  id Int @id @default(autoincrement())
  date DateTime @default(now())
  categoryUserId Int
  categoryName String
  category Category @relation(fields: [categoryUserId, categoryName], references: [userId, name])
  tags Tag[]
  type String
  description String
  amount Float
  notes String
  userId Int
  user User @relation(fields: [userId], references: [id])
psl=}


route RootRoute { path: "/", to: MainPage }
page MainPage {
  authRequired: true,
  component: import Main from "@client/MainPage.tsx"
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@client/pages/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@client/pages/SignupPage"
} 

query getTransactions {
  fn: import { getTransactions } from "@server/queries.js",
  entities: [Transaction]
}

query getCategories {
  fn: import { getCategories } from "@server/queries.js",
  entities: [Category]
}

query getTags {
  fn: import { getTags } from "@server/queries.js",
  entities: [Tag]
}

action createCategory {
  fn: import { createCategory } from "@server/actions.js",
  entities: [Category]
}

action createTag {
  fn: import { createTag } from "@server/actions.js",
  entities: [Tag]
}

action createTransaction {
  fn: import { createTransaction } from "@server/actions.js",
  entities: [Transaction]
}

action deleteTransaction {
  fn: import { deleteTransaction } from "@server/actions.js",
  entities: [Transaction]
}
